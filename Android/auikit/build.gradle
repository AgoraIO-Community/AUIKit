apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'
apply plugin: 'de.undercouch.download'

import de.undercouch.gradle.tasks.download.Download

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 30

        consumerProguardFiles "consumer-rules.pro"
        ndk.abiFilters 'arm64-v8a', 'armeabi-v7a' //, 'x86', 'x86-64'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    buildFeatures {
        viewBinding true
    }
    sourceSets {
        main {
            jniLibs.srcDirs += "libs"
        }
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])

    // Unit Test Libraries
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    // androidTestImplementation 'androidx.test.espresso:espressso-core:3.4.0'

    // Android Component Libraries
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.core:core-ktx:1.6.0'

    // Kotlin External Libraries
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1'

    // Logging Libraries
    implementation 'com.orhanobut:logger:2.2.0'

    // Http Libraries
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'
    api 'com.squareup.retrofit2:retrofit:2.9.0'
    api 'com.squareup.retrofit2:converter-gson:2.9.0'
    api 'com.squareup.okhttp3:logging-interceptor:4.10.0'
    api 'com.google.code.gson:gson:2.9.0'
    implementation 'com.github.mrmike:ok2curl:0.8.0'
    implementation 'com.google.code.gson:gson:2.9.0'

    // Image Loading Libraries
    implementation 'com.github.bumptech.glide:glide:4.15.0'
    kapt 'com.github.bumptech.glide:compiler:4.15.0'

    // Ui Libraries
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.github.AgoraIO-Community:LyricsView:1.1.1-beta.8'

    //agora chat
    api 'io.agora.rtc:chat-sdk:1.1.0'

    api project(':auikit-ui')
}

task downloadZipFile(type: Download) {
    src 'https://download.agora.io/null/Agora_Native_SDK_for_Android_rel.v4.1.1.30_49294_FULL_20230512_1606_264137.zip'
    dest new File(buildDir, '1.0.zip')
    overwrite false
}

task downloadAndUnzipFile(dependsOn: downloadZipFile, type: Copy) {
    if(downloadZipFile.dest.exists()){
        def fileTree = zipTree(downloadZipFile.dest)
        def rtcJar = fileTree.getFiles().find {
            it.name.contains("agora-rtc-sdk.jar")
        }
        println "rtcJar path= $rtcJar.parentFile.absolutePath"
        from rtcJar.parentFile
        into 'libs'
    }
}

preBuild.dependsOn downloadAndUnzipFile
// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'io.github.agoraio-community'
                artifactId = 'AUIKit'
                version = '0.2.0'
            }
            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                groupId = 'io.github.agoraio-community'
                artifactId = 'AUIKit'
                version = '0.2.0'
            }
        }
    }
}